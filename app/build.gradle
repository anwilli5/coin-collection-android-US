plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android' version '2.2.0' apply false
}

android {
    signingConfigs {
        Android {
            def signingPropsFile = new File('../signing.properties')
            if (signingPropsFile.canRead()) {
                def props = new Properties()
                def fileInputStream = new FileInputStream(signingPropsFile)
                props.load(fileInputStream)
                fileInputStream.close()
                storeFile = file(props['storeFilePath'])
                storePassword = props['storePassword']
                keyAlias = props['keyAlias']
                keyPassword = props['keyPassword']
            }
        }
    }
    compileSdk 35
    // minSdkVersion should be the lowest supported.
    //  - For app versions 1.6 and below this was 4
    //  - Raised to 7 for version 2.0 to support the appcompat support library
    //    so we could use ActionBars
    //  - Raised to ?? for version 2.2.1 to get the recyclerview (for the
    //    view to reorder collections
    //  - Raised to 14 for version 2.3.4 since appcompat's min SDK increased to that
    // targetSdkVersion should be raised to match the latest version of
    // Android and then tested (on that version) to make sure it works.
    defaultConfig {
        applicationId "com.spencerpages"
        minSdkVersion 21
        targetSdkVersion 35
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "app-${variant.flavorName}-${variant.buildType.name}-v${variant.versionName}.apk"
                }
            }
        }
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
    }
    flavorDimensions "version"
    productFlavors {
        android {
            dimension "version"
        }
        amazon {
            dimension "version"
            applicationIdSuffix ".amazon"
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    lint {
        abortOnError false
    }
    namespace 'com.spencerpages'
    buildFeatures {
        buildConfig true
    }
    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            testLogging {
                events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
                exceptionFormat "full"
                showExceptions = true
                showCauses = true
                showStackTraces = true
                showStandardStreams = true
            }
            // Configure JVM arguments to prevent Mockito self-attachment warnings
            jvmArgs '-XX:+EnableDynamicAgentLoading'
        }
        unitTests.includeAndroidResources = true
    }
    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles.
        includeInBundle = false
    }
    packagingOptions {
        merge 'META-INF/LICENSE.md'
        merge 'META-INF/LICENSE-notice.md'
    }
}

configurations {
    configureEach {
        exclude module: 'commons-logging'
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'com.opencsv:opencsv:5.12.0'
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:2.2.0"))
    //debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.6'
    testImplementation(project(":shared-test"))
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.15.1'
    testImplementation 'androidx.test:core:1.7.0'
    testImplementation 'org.mockito:mockito-core:5.18.0'
    androidTestImplementation(project(":shared-test"))
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
    androidTestImplementation 'androidx.test:runner:1.7.0'
    androidTestImplementation 'androidx.test:rules:1.7.0'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.3.0'
    androidTestImplementation 'tools.fastlane:screengrab:2.1.1'
}